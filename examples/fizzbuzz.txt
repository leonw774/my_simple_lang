concat_linked_list = list_pair : {
    left_list = `list_pair;
    right_list = ~list_pair;
    left_empty = (left_list == null);
    left_empty ? (new_list = right_list);
    !left_empty ? (
        left_list_next = ~left_list;
        end = (left_list_next == null);
        end ? (
            new_list = `left_list, right_list
        );
        !end ? (
            new_list = (
                `left_list,
                concat_linked_list(left_list_next, ~list_pair)
            )
        )
    );
    new_list
};

print = string : {
    << `string;
    (~string != null) ? print(~string)
};

format_int = i : {
    end = i < 10;
    res_str = null;
    end ? (
        res_str = (i + '0'), null
    );
    !end ? (
        left = format_int((i - i % 10) / 10);
        right = ((i % 10) + '0'), null;
        res_str = concat_linked_list(left, right)
    );
    res_str
};

fizz = 'F', 'I', 'Z', 'Z', null;
buzz = 'B', 'U', 'Z', 'Z', null;

fizzbuzz = n : {
    end = (n == 1); 
    !end ? fizzbuzz(n-1);
    res = null;
    (n % 3 == 0) ? (res = concat_linked_list(res, fizz));
    (n % 5 == 0) ? (res = concat_linked_list(res, buzz));
    (res == null) ? print(format_int(n));
    (res != null) ? print(res);
    << '\n'
};

fizzbuzz(15)
